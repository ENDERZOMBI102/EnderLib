plugins {
	id 'org.quiltmc.loom' version '0.12.+'
	id 'maven-publish'
}

description = 'EnderLib module with many minecraft utilities to be used with FabricMC ( jvm-17 )'

repositories {
	maven { url 'https://maven.fabricmc.net' }
	maven { url 'https://maven.terraformersmc.com' }
	maven { url 'https://ladysnake.jfrog.io/artifactory/mods' }
	maven { url 'https://maven.quiltmc.org/repository/release' }
	maven { url 'https://maven.quiltmc.org/repository/shanpshot' }
}

dependencies {
	minecraft "com.mojang:quilt:$minecraft_version"
	mappings loom.layered {
		addLayer quiltMappings.mappings( "org.quiltmc:quilt-mappings:$minecraft_version+build.$quilt_mappings:v2" )
	}
	modImplementation "org.quiltmc:quilt-loader:$loader_version"

	modImplementation "org.quiltmc:qsl:$qsl_version+$minecraft_version"
	modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:$qfapi_version-$minecraft_version-SNAPSHOT"

	modCompileOnly modRuntimeOnly( "com.terraformersmc:modmenu:$modmenu_version" ) {
		transitive = false
	}
}

loom.runConfigs.client.runDir = '../run'
loom.runConfigs.server.runDir = '../run'

processResources {
	inputs.property 'repo', repo
	inputs.property 'version', version
	inputs.property 'description', project.description
	filteringCharset 'UTF-8'

	filesMatching 'fabric.mod.json', {
		expand(
			'repo': repo,
			'version': version,
			'description': project.description
		)
	}
}

tasks.withType( JavaCompile ).configureEach {
	it.sourceCompatibility = 17
	it.targetCompatibility = 17
	it.options.encoding = 'UTF-8'
}

jar {
	from 'LICENSE', {
		rename { "${it}_$archivesBaseName" }
	}
	manifest.attributes += [
		'Implementation-Title'    : 'EnderLib Quilt',
		'Implementation-Version'  : archiveVersion,
		'Implementation-Vendor'   : 'ENDERZOMBI102',
		'Implementation-Timestamp': new Date().format("dd-MM-yyyy+HH:mm:ssZ")
	]
}

java {
	withJavadocJar()
	withSourcesJar()
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			group 'com.enderzombi102.EnderLib'
			artifactId 'quilt'
			from components.java
		}
	}

	repositories {
		maven {
			name 'Repsy'
			credentials(PasswordCredentials)
			url 'https://repsy.io/mvn/enderzombi102/mc'
		}
	}
}
