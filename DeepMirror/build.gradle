plugins {
    id 'maven-publish'
    id 'java'
    id 'idea'
}

description = 'DeepMirror is a deep reflection library, that can basically take over the JVM'
version = deepmirror_version

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains:annotations:$annotations_version"
    compileOnly "org.apache.logging.log4j:log4j-slf4j18-impl:$log4j_version"
}

tasks.withType(JavaCompile).configureEach {
    it.sourceCompatibility = 17
    it.targetCompatibility = 17
    it.options.encoding = 'UTF-8'
    it.options.compilerArgs = [
        '--add-modules', 'jdk.hotspot.agent',
        '--add-exports', 'jdk.hotspot.agent/sun.jvm.hotspot.runtime=ALL-UNNAMED'
    ]
}

jar {
    from 'LICENSE', {
        rename { "${it}_$archivesBaseName" }
    }
    manifest.attributes += [
        'Implementation-Title'    : 'DeepMirror',
        'Implementation-Version'  : archiveVersion,
        'Implementation-Vendor'   : 'ENDERZOMBI102',
        'Implementation-Timestamp': new Date().format("dd-MM-yyyy+HH:mm:ssZ")
    ]
}

java {
    withJavadocJar()
    withSourcesJar()
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            group 'com.enderzombi102'
            artifactId 'DeepMirror'
            from components.java
        }
    }

    repositories {
        maven {
            name 'Repsy'
            credentials(PasswordCredentials)
            url 'https://repsy.io/mvn/enderzombi102/mc'
        }
    }
}
