plugins {
    id 'maven-publish'
    id 'java'
    id 'idea'
}

description = 'EnderLib for java 8, as the normal one is compiled for java 17'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains:annotations:$annotations_version"
    compileOnly "org.apache.logging.log4j:log4j-slf4j18-impl:$log4j_version"
}

tasks.withType(JavaCompile).configureEach {
    it.sourceCompatibility = 8
    it.targetCompatibility = 8
    it.options.encoding = 'UTF-8'
}

jar {
    from 'LICENSE', {
        rename { "${it}_$archivesBaseName" }
    }
    manifest.attributes += [
        'Implementation-Title'    : 'EnderLib JVM 8',
        'Implementation-Version'  : archiveVersion,
        'Implementation-Vendor'   : 'ENDERZOMBI102',
        'Implementation-Timestamp': new Date().format("dd-MM-yyyy+HH:mm:ssZ")
    ]
}

java {
    withJavadocJar()
    withSourcesJar()
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            group 'com.enderzombi102.EnderLib'
            artifactId 'jvm8'
            from components.java
        }
    }

    repositories {
        maven {
            name 'Repsy'
            credentials(PasswordCredentials)
            url 'https://repsy.io/mvn/enderzombi102/mc'
        }
    }
}
